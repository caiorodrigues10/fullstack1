plugins {
    id 'eclipse'
    id 'maven-publish'
    id 'jacoco'

	id 'java'
	id 'org.springframework.boot' version '3.5.5'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'br.com.jtech.tasklist'
version = APP_VERSION
description = 'Demo project for Spring Boot'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

// Configura encoding para evitar problemas com caracteres especiais
tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
	options.compilerArgs += ['-parameters']
	options.annotationProcessorPath = configurations.annotationProcessor
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
	// Repositório da JTech (requer autenticação - comentado para desenvolvimento local)
	// maven { url "https://nexus.jtech.com.br/repository/maven-public" }
}

dependencies {
	implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:2.0.4"
	compileOnly "org.projectlombok:lombok"
	annotationProcessor "org.projectlombok:lombok"
	implementation "org.springframework.boot:spring-boot-starter-web"
	implementation "org.hibernate.validator:hibernate-validator:8.0.0.Final"
	testImplementation "org.assertj:assertj-core:3.24.2"
	testImplementation "com.google.code.bean-matchers:bean-matchers:0.14"
	testImplementation "org.junit.platform:junit-platform-suite-engine:1.9.2"
	testRuntimeOnly "com.h2database:h2"
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'com.h2database:h2'
	runtimeOnly 'org.postgresql:postgresql'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
}

// Corrige problema com caminhos com caracteres especiais (ex: "Área de Trabalho")
// OneDrive pode causar problemas com rastreamento de arquivos
tasks.named('processResources', ProcessResources) {
	doNotTrackState("Caminho contém caracteres especiais e OneDrive pode causar problemas")
	duplicatesStrategy = DuplicatesStrategy.INCLUDE
	// Força processamento mesmo com problemas de snapshot
	outputs.upToDateWhen { false }
}

// Corrige problema de snapshot com OneDrive na compilação Java
tasks.named('compileJava', JavaCompile) {
	// Desabilita build incremental para evitar problemas com OneDrive
	options.incremental = false
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = group
            artifactId = rootProject.name
            version = version
            from components.java
        }
    }
    repositories {
        maven {
            name 'nexus'
            url = 'https://nexus.jtech.com.br/repository/maven-' + (version.endsWith('SNAPSHOT') ? 'snapshots/' : 'releases/')
            credentials {
                username System.getenv("MAVEN_REPO_USER")
                password System.getenv("MAVEN_REPO_PASS")
            }
        }
    }
}
